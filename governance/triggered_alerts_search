#############################################################################
## Searchs to find the triggered alerts and saved searches.
## Created by: Rafael Santos
## Date: 09/19/2019
## Don't forget to set the time range to run this search, i.e.: @d, 1h, 5m
#############################################################################

#############################################################################
## Search to see the alerts was triggered no Splunk #########################
#############################################################################
index=_audit action="alert_fired" ss_app="[app_name]"
| rename ss_name AS title
| join title [ | rest /servicesNS/-/-/saved/searches splunk_server=local | table title, alert_threshold, severity ]
| where like(alert_actions, "%script%") AND triggered_alerts > 0
| rename title AS Rule
| stats values(severity) AS Severity, values(ss_app) AS Customer, sum(triggered_alerts) AS Eventos, count AS Incident by Rule
| addcoltotals label=Total labelfield=Rule
| sort -Severity, -count

#############################################################################
## Search how many times a specific alert/saved search was triggered ########
## Don't forget to set the time range to run this search, i.e.: @d, 1h, 5m
## Don't forget to set name of the ss_name
#############################################################################
index=_audit action="alert_fired" ss_name="[name of alert/saved search]" triggered_alerts > 0

#############################################################################
## alerts triggered by app ##################################################
#############################################################################
| rest /servicesNS/-/-/saved/searches splunk_server=local
| search disabled=1 eai:acl.app="[app_name]" action.script.filename!="" title="[title]"

#############################################################################
## See the results of SLA Severity defined by the client ####################
#############################################################################
index=_audit action="alert_fired" ss_app=[app_name]
| rename ss_name AS title
| join title [ | rest /servicesNS/-/-/saved/searches splunk_server=local | table title, alert_threshold, search, severity ]
| where like(alert_actions, "%script%") AND triggered_alerts > 0
| rename title AS Rule
## Optional regex ##
| rex field=search "\_Severity\=\"(?<SevSLA>\d)\""
| stats values(severity) AS Severity, values(ss_app) AS Customer, sum(triggered_alerts) AS Eventos, count AS Incident by Rule, SevSLA
| addcoltotals label=Total labelfield=Rule
| sort -Severity, -count

index=_audit action="alert_fired" ss_app=[app_name]
| rename ss_name AS title
| join title [ | rest /servicesNS/-/-/saved/searches splunk_server=local | table title, alert_threshold, search, severity, actions]
| rename title AS Rule
| stats values(severity) AS Severity, values(ss_app) AS Customer, sum(triggered_alerts) AS Eventos, count AS Incident by Rule, actions
| addcoltotals label=Total labelfield=Rule
| sort Rule, -Severity, -count

#############################################################################
## Verify the Alerts that uses to the script to fire ##
#############################################################################

| rest /servicesNS/-/-/saved/searches splunk_server=local 
| search eai:acl.app=[app_name] disabled=0 action.script!="" next_scheduled_time!="" action.script.filename=[script_name] next_scheduled_time > "2019-10-10 00:00:00 -03"
| rename eai:acl.app AS Customer, eai:acl.owner AS Owner
| table Customer, title, Owner, search, next_scheduled_time, action.script, action.script.filename
| sort title

## Consult specific alerts created - Enabled or non-enabled ##
| rest /servicesNS/-/-/saved/searches splunk_server=local 
| search eai:acl.app=[app_name]
| rename eai:acl.app AS Customer, eai:acl.owner AS Owner
| replace admin with "owner_name" in Owner 
| eval status=if(disabled=1, "Disabled", "Enabled")
| search title="String_to_Consult*"
| table Customer, title, Owner, search, status
| sort title

###############################
## List of triggered alerts  ##
## Sorted by triggered_count ##
###############################

| rest /servicesNS/-/-/saved/searches
| table eai:acl.owner eai:acl.app id title triggered_alert_count
| sort - triggered_alert_count
